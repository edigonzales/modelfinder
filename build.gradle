plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'gg.jte.gradle' version '3.1.16'
}

apply from: "$rootDir/gradle/versioning.gradle"
//apply from: "$rootDir/gradle/docker.gradle"

group = 'ch.so.agi'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jars.interlis.ch/' }
    maven { url "https://s01.oss.sonatype.org/service/local/repositories/releases/content/" }
}

configurations.all {
    resolutionStrategy {
        force 'ch.interlis:ili2c-tool:5.6.4'
        force 'ch.interlis:ili2c-core:5.6.4'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'gg.jte:jte-spring-boot-starter-3:3.1.16'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    implementation 'ch.interlis:ili2c-tool:5.6.4'
    implementation 'ch.interlis:ili2c-core:5.6.4'
    // Auch nur wegen fehlender lokalen Libs, die prettyprinter ben√∂tigt (uml editor).
    implementation 'io.github.sogis:iliprettyprint:0.0.67:all@jar'
    
    implementation 'org.apache.lucene:lucene-core:10.2.1'
    implementation 'org.apache.lucene:lucene-queryparser:10.2.1'
}

jte {
    generate()
    binaryStaticContent = true
}

tasks.named('test') {
    useJUnitPlatform()
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            buildArgs.add('-march=compatibility')
            verbose = true
        }
    }
}
