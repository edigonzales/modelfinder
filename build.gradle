plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'gg.jte.gradle' version '3.1.16'
}

apply from: "$rootDir/gradle/versioning.gradle"
//apply from: "$rootDir/gradle/docker.gradle"

group = 'ch.so.agi'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url "https://jars.sogeo.services/mirror" }
    maven { url "https://s01.oss.sonatype.org/service/local/repositories/releases/content/" }
}

configurations.all {
    resolutionStrategy {
        force 'ch.interlis:ili2c-tool:5.6.3'
        force 'ch.interlis:ili2c-core:5.6.3'
    }
    exclude group: 'net.sourceforge.plantuml', module: 'plantuml'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'info.picocli'
    exclude group: 'com.sun.xml.bind'
    exclude group: 'javax.xml.bind'
    
    //exclude group: 'commons-logging', module: 'commons-logging'
    //exclude group: 'ch.qos.logback', module: 'logback-classic' 
    //exclude group: 'ch.qos.logback', module: 'logback-core'    
    
    // Bringt wohl nichts, weil eh nicht vorhanden.
    //exclude group: 'org.apache.lucene', module: 'lucene-core-tests'
    //exclude group: 'org.apache.lucene', module: 'lucene-core-test-framework'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'gg.jte:jte-spring-boot-starter-3:3.1.16'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    implementation 'ch.interlis:ili2c-tool:5.6.3'
    implementation 'ch.interlis:ili2c-core:5.6.3'
    implementation 'ch.interlis:iox-ili:1.23.4'
    implementation ('io.github.sogis:iliprettyprint:0.0.67') {
        exclude group: 'ch.interlis', module: 'umleditor'
    }
    //implementation name: 'sebase'
    //implementation name: 'jhotdraw53'
    //implementation name: 'XMIFramework'
    //implementation name: 'umleditor-3.10.3'
    implementation fileTree(dir: 'lib', include: ['*.jar'])
    
    implementation 'org.apache.lucene:lucene-core:8.11.4' // 9.9.2 scheint die letzte zu sein, ohne Foreign Memory API
    implementation('org.apache.lucene:lucene-queryparser:8.11.4') {
        exclude group: 'org.apache.lucene', module: 'lucene-sandbox' // Bug in Lucene und führt zu Problemen mit GraalVM.
    }
}

jte {
    generate()
    binaryStaticContent = true
}

tasks.named('test') {
    useJUnitPlatform()
}

import org.springframework.boot.gradle.tasks.aot.ProcessAot
tasks.withType(ProcessAot) {
    args "--spring.profiles.active=${project.findProperty('aotProfiles') ?: 'default'}"
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            buildArgs.add('-march=compatibility')
            buildArgs.add('-Djava.awt.headless=false')
            verbose = true
            quickBuild = true // Enables quick build mode
            //buildArgs.add('--exclude=org.apache.lucene.internal.tests.TestSecrets')
            //buildArgs.add("-H:+ForeignAPISupport") // AMD64 only. Ggf. für Lucene ab 9.10.0
            //buildArgs.addAll('--initialize-at-run-time=org.slf4j.helpers.Reporter,org.slf4j.LoggerFactory,org.apache.commons.logging.LogFactory')
            //buildArgs.add('--trace-class-initialization=org.slf4j.helpers.Reporter,org.slf4j.LoggerFactory,org.apache.commons.logging.LogFactory')
        }
    }
}
